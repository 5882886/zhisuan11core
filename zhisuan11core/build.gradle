plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.zhisuan11'
version = '1.2'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

// 依赖库管理
dependencies {
    // Spigot api版本
    compileOnly("org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT")
    // MySQL 数据库
    implementation 'mysql:mysql-connector-java:8.0.33'
    // HikariCP 高性能数据库连接池
    implementation 'com.zaxxer:HikariCP:5.0.1'

    /* 以下是可选内容 */
    // SQLite JDBC
    // implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
    // SLF4J - HikariCP的依赖
    // implementation 'org.slf4j:slf4j-nop:2.0.9'
}

tasks {
  runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.20")
  }
}

shadowJar {
    // 重命名依赖包，避免冲突
    relocate 'com.zaxxer.hikari', 'org.zhisuan11.libs.hikari'
    // 命名输出文件
    archiveFileName = "${project.name}-${project.version}.jar"
    // 可选：最小化包含的内容（只包含用到的类）
    minimize()
}

tasks.build {
    dependsOn tasks.shadowJar
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
